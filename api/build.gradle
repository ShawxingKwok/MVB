import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    alias(libs.plugins.publish)
}

android {
    namespace 'pers.shawxingkwok.mvb.android'
    compileSdk libs.versions.compileSdk.get().toInteger()

    defaultConfig {
        minSdk libs.versions.minSdk.get().toInteger()
        targetSdk libs.versions.targetSdk.get().toInteger()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation libs.androidx.coreKtx
    implementation libs.appcompat
    implementation libs.material
    implementation(libs.bundles.shawxing)
    implementation libs.androidx.lifecycleRuntimeKtx
    implementation libs.lifecycle.viewmodel.ktx

    testImplementation libs.junit
    androidTestImplementation libs.androidx.test.ext.junit
    androidTestImplementation libs.espresso.core
}

tasks.withType(KotlinCompile).configureEach{
    kotlinOptions.freeCompilerArgs += [
        "-Xexplicit-api=warning",
        "-Xcontext-receivers"
    ]
}

// publish
mavenPublishing {
    def artifactId = "mvb-android"
    def version = "1.0.0"
    def isSnapshot = true

    if (isSnapshot) version += "-SNAPSHOT"
    pom {
        coordinates("io.github.shawxingkwok", artifactId, version)
        def repoName = "MVB"
        name = repoName
        description = "MVB architecture on Android"
        inceptionYear = "2023"

        url = "https://github.com/ShawxingKwok/$repoName/"

        scm{
            connection = "scm:git:git://github.com/ShawxingKwok/${repoName}.git"
            developerConnection = "scm:git:ssh://git@github.com/ShawxingKwok/${repoName}.git"
        }
    }
}